buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/snapshots'
        }
    }

    dependencies {
        classpath 'net.minecrell:IceGradle:0.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
        //classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

apply plugin: 'net.minecrell.ice'
//apply plugin: 'license'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'net.minecrell.ice.launch.Ice'

ext {
    mixinSrg = new File(project.buildDir, "tmp/mixins/mixins.srg")
    mixinRefMap = new File(project.buildDir, "tmp/mixins/mixins.quartz.refmap.json")
}

minecraft {
    version = '1.8'
    mappings = 'snapshot_20150216'
    tweaker = 'net.minecrell.ice.launch.IceTweaker'
}

dependencies {
    compile 'org.spongepowered:mixin:0.1-SNAPSHOT'
    runtime 'org.ow2.asm:asm-all:5.0.3'
}

shadowJar {
    classifier = ''
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'

    dependencies {
        include dependency('org.spongepowered:mixin')
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')
    }

    from project.mixinRefMap
}
build.dependsOn(shadowJar)

jar {
    manifest {
        attributes(
                'Class-Path': 'bin/minecraft_server.1.8.jar bin/launchwrapper-1.11.jar'
        )
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:all',
            '-Xlint:-path',
            '-Xlint:-processing',
            "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
            "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}"
    ]

    options.deprecation = true
    options.encoding = 'utf8'
}

// License header formatting
/*license {
    header file('LICENSE')
    exclude '*.txt'
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}*/

// Copy the current srgs to the build dir so the user doesn't have to go hunting for them
task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from plugins.getPlugin('net.minecrell.ice').delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}

setupDecompWorkspace.dependsOn copySrgs
setupDevWorkspace.dependsOn copySrgs

reobf {
    addExtraSrgFile project.mixinSrg

    reobf(shadowJar) { spec ->
        spec.classpath = configurations.compile
    }
}

afterEvaluate {
    tasks.compileJava.options.compilerArgs += "-AreobfSrgFile=${tasks.reobf.srg}"
}
