buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/snapshots'
        }
    }

    dependencies {
        classpath 'net.minecrell:QuartzGradle:0.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
        //classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'sonatype-snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

apply plugin: 'net.minecrell.quartz'
//apply plugin: 'license'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'net.minecrell.quartz.launch.QuartzMain'

ext {
    mixinSrg = new File(project.buildDir, "tmp/mixins/mixins.srg")
    mixinRefMap = new File(project.buildDir, "tmp/mixins/mixins.quartz.refmap.json")
}

minecraft {
    version = '1.8'
    mappings = 'snapshot_20150219'
    tweaker = 'net.minecrell.quartz.launch.QuartzTweaker'
}

dependencies {
    compile project('SpongeAPI')
    compile('org.spongepowered:mixin:0.1-SNAPSHOT') {
        exclude module: 'launchwrapper'
    }

    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
}

compileJava {
    options.compilerArgs += [
            '-Xlint:all',
            '-Xlint:-path',
            '-Xlint:-processing',
            "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
            "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}"
    ]

    options.deprecation = true
    options.encoding = 'utf8'
}

afterEvaluate {
    tasks.compileJava.options.compilerArgs += "-AreobfSrgFile=${tasks.reobf.srg}"
}

jar {
    manifest {
        attributes(
                'Class-Path': 'bin/minecraft_server.1.8.jar bin/launchwrapper-1.11.jar'
        )
    }
}

shadowJar {
    classifier = ''
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'

    dependencies {
        include dependency('org.spongepowered:mixin')
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        // SpongeAPI
        include project('SpongeAPI')

        include dependency('org.slf4j:slf4j-api')
        include dependency('org.apache.logging.log4j:log4j-slf4j-impl')

        include dependency('com.google.inject:guice')
        include dependency('aopalliance:aopalliance:1.0')
        include dependency('javax.inject:javax.inject')

        include dependency('com.typesafe:config')
        include dependency('ninja.leaping.configurate:configurate-core')
        include dependency('ninja.leaping.configurate:configurate-hocon')

        include dependency('com.flowpowered:flow-math')
    }

    from project.mixinRefMap
}
build.dependsOn(shadowJar)

// License header formatting
/*license {
    header file('LICENSE')
    exclude '*.txt'
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}*/

// Copy the current srgs to the build dir so the user doesn't have to go hunting for them
task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from plugins.getPlugin('net.minecrell.quartz').delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}

setupDecompWorkspace.dependsOn copySrgs
setupDevWorkspace.dependsOn copySrgs

reobf {
    addExtraSrgFile project.mixinSrg

    reobf(shadowJar) { spec ->
        spec.classpath = configurations.compile
    }
}

configure([distZip, distTar]) {
    enabled = false
}

// Disable checkstyle and Javadocs for SpongeAPI
project('SpongeAPI') {
    afterEvaluate {
        configure([javadoc, javadocJar, sourceJar, licenseFormat]) {
            enabled = false
        }
        tasks.withType(Checkstyle) {
            enabled = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
