plugins {
    id 'java'
    id 'application'

    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

repositories {
    mavenCentral()
    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net'
    }
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'sonatype-snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

version = '0.1-SNAPSHOT'

mainClassName = 'net.minecrell.quartz.launch.QuartzMain'

apply from: file('minecraft.gradle')

dependencies {
    compile project('SpongeAPI')

    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    runtime 'org.ow2.asm:asm-all:5.0.3'

    compile('org.spongepowered:mixin:0.2-SNAPSHOT') {
        transitive = false
    }

    compile 'net.sf.jopt-simple:jopt-simple:4.6'
    compile('net.minecraft:launchwrapper:1.11') {
        transitive = false
    }

    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
}

compileJava {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-deprecation']
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Specification-Name': 'SpongeAPI',
                'Specification-Version': 'TODO',
                'Specification-Vendor': 'SpongePowered',
                'Implementation-Name': name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Minecrell',
                'Class-Path': 'bin/minecraft_server.1.8.3.jar bin/launchwrapper-1.11.jar'
        )
    }
}

shadowJar {
    classifier = ''
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'

    dependencies {
        include dependency('org.spongepowered:mixin')
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')

        // SpongeAPI
        include project('SpongeAPI')

        include dependency('org.slf4j:slf4j-api')
        include dependency('org.apache.logging.log4j:log4j-slf4j-impl')

        include dependency('com.google.inject:guice')
        include dependency('aopalliance:aopalliance:1.0')
        include dependency('javax.inject:javax.inject')

        include dependency('com.typesafe:config')
        include dependency('ninja.leaping.configurate:configurate-core')
        include dependency('ninja.leaping.configurate:configurate-hocon')

        include dependency('com.flowpowered:flow-math')
    }
}
build.dependsOn(shadowJar)

// License header formatting
license {
    header file('LICENSE')
    exclude '*.txt'
    exclude '*.xml'
    exclude '*.json'
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}


configure([distZip, distTar]) {
    enabled = false
}

// Disable checkstyle and Javadocs for SpongeAPI
project('SpongeAPI') {
    afterEvaluate {
        configure([javadoc, javadocJar, sourceJar, licenseFormat]) {
            enabled = false
        }
        tasks.withType(Checkstyle) {
            enabled = false
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
